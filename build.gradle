plugins {
	id "java-gradle-plugin"
	id "groovy"
	id "maven-publish"
	id "com.palantir.launch-config" version "1.2.0"
}

group = "info.u-team.gradle_files"
archivesBaseName = "gradle_files"
version = "1.0.0"
description = "Gradle plugin for special gradle files used for mods by u-team"

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
	withSourcesJar()
	withJavadocJar()
}

sourceSets {
	main {
		java {
			srcDirs = []
		}
		groovy {
			srcDirs = [
				"src/main/java",
				"src/main/groovy"
			]
		}
	}
}

gradlePlugin {
	plugins {
		gradleFilesPlugin {
			id = "info.u_team.gradle_files"
			implementationClass = "info.u_team.gradle_files_plugin.GradleFilesPlugin"
		}
	}
}

repositories {
	maven {
		url = "https://maven.minecraftforge.net"
	}
}

dependencies {
	implementation group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
}

jar.manifest.mainAttributes([
	"Implementation-Title": project.name,
	"Implementation-Version": project.version
])

[compileJava, compileGroovy].each {
	it.options.encoding = "UTF-8"
	it.options.deprecation = true
	it.options.fork = true
}

task runPluginTest(type: JavaExec) {
	def run = file("${rootDir}/test")
	
	workingDir  run
	main "info.u_team.gradle_files_plugin.test.GradleFilesPluginTest"
	systemProperties = [
		"gradle.project_dir": "\"${run}\""
	]
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact jar
		}
	}
	repositories {
		maven {
			url "file:///${buildDir}/mavenRepo"
		}
	}
}